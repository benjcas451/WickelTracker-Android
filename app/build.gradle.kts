import java.util.Properties

plugins {
    alias(libs.plugins.android.application)
    // Compose-Compiler; Kotlin selbst kommt ueber AGPs Built-in Kotlin.
    alias(libs.plugins.kotlin.compose)
}

// Signing-Daten aus key.properties im Projekt-Root laden (nicht im Git,
// siehe .gitignore und key.properties.example). Derselbe Upload-Key wie bei
// der bisherigen Flutter-App – Play verlangt fuer Updates derselben
// applicationId denselben Schluessel.
val keystoreProperties = Properties()
val keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.inputStream().use { keystoreProperties.load(it) }
}

android {
    namespace = "org.dwarftsch.wickel"
    compileSdk {
        version = release(37)
    }

    defaultConfig {
        // Gleiche applicationId wie die bisherige Flutter-App: die native App
        // ersetzt sie unter demselben Play-Eintrag – Bestandsdaten und
        // -einstellungen bleiben beim Update erhalten.
        applicationId = "org.dwarftsch.wickel"
        minSdk = 24
        targetSdk = 37
        // Muss beim Play-Upload strikt ueber dem letzten veroeffentlichten
        // versionCode der Flutter-App liegen (zuletzt 10). Die CI uebergibt
        // -PbuildNumber=100+run_number (siehe .github/workflows/build-aab.yml),
        // lokal gilt der Fallback.
        versionCode = (findProperty("buildNumber") as String?)?.toIntOrNull() ?: 11
        versionName = "2.0.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        create("release") {
            if (keystorePropertiesFile.exists()) {
                keyAlias = keystoreProperties["keyAlias"] as String
                keyPassword = keystoreProperties["keyPassword"] as String
                // Absolute Pfade unveraendert, relative relativ zum Root.
                val configured = File(keystoreProperties["storeFile"] as String)
                storeFile = if (configured.isAbsolute) configured else rootProject.file(configured.path)
                storePassword = keystoreProperties["storePassword"] as String
            }
        }
    }

    buildTypes {
        release {
            // R8 verkleinert das Bundle deutlich und optimiert den Bytecode;
            // das wear-Modul nutzt es laengst (dort gemessen: 7,0 -> 2,9 MB).
            // Play verlangt die Codeoptimierung nun auch fuer die Handy-App.
            // Keep-Regeln: src/main/keepRules/rules.keep. Die Mapping-Datei
            // legt AGP automatisch ins AAB.
            optimization {
                enable = true
            }
            // Ohne key.properties mit dem Debug-Key signieren, damit sich ein
            // Release-Build lokal auch ohne Keystore erzeugen laesst.
            signingConfig = if (keystorePropertiesFile.exists()) {
                signingConfigs.getByName("release")
            } else {
                signingConfigs.getByName("debug")
            }
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        // java.time auf minSdk 24 (API < 26) braucht Library-Desugaring.
        isCoreLibraryDesugaringEnabled = true
    }
    buildFeatures {
        compose = true
    }
    testOptions {
        unitTests {
            // Die Regressionstests fuer den Einstellungen-Bildschirm laufen
            // ueber Robolectric auf der JVM und brauchen echte Ressourcen.
            isIncludeAndroidResources = true
        }
    }
}

dependencies {
    coreLibraryDesugaring(libs.desugar.jdk.libs)

    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.core.ktx)
    implementation(libs.material)

    // Haelt alle androidx.compose.*-Artefakte auf demselben Stand. Ohne die
    // BOM zogen activity-compose und lifecycle ui/runtime auf 1.9.x, waehrend
    // material3 foundation/animation auf 1.7.0 hielt.
    implementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(platform(libs.androidx.compose.bom))
    testImplementation(platform(libs.androidx.compose.bom))

    implementation(libs.androidx.activity.compose)
    implementation(libs.androidx.compose.material3)
    implementation(libs.androidx.compose.foundation)
    implementation(libs.androidx.compose.ui)
    implementation(libs.androidx.compose.icons.extended)
    implementation(libs.androidx.lifecycle.viewmodel.compose)
    implementation(libs.kotlinx.coroutines.android)

    // REST-API (unterstuetzt mTLS, anders als HttpURLConnection).
    implementation(libs.okhttp)
    // Data-Layer-API: Anfragen der Wear-OS-App (WearRequestService).
    implementation(libs.play.services.wearable)
    // Zertifikats-Ordner via Storage Access Framework.
    implementation(libs.androidx.documentfile)

    testImplementation(libs.junit)
    testImplementation(libs.robolectric)
    testImplementation(libs.androidx.test.core.ktx)
    testImplementation(libs.androidx.junit)
    testImplementation(libs.androidx.compose.ui.test.junit4)
    debugImplementation(libs.androidx.compose.ui.test.manifest)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(libs.androidx.junit)
}
